
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ЦеныНоменклатуры.Записывать = Истина;
	
	Для каждого СтрокаТовары Из Товары Цикл
		НС = Движения.ЦеныНоменклатуры.Добавить();
		НС.Период = Дата;
		НС.Номенклатура = СтрокаТовары.Номенклатура;
		НС.Контрагент = Контрагент;
		НС.ЦенаПродажи = СтрокаТовары.ЦенаПродажи;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// проверим, может на этот день уже установлены цены
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	               |	ЦеныНоменклатуры.Период КАК Период,
	               |	ЦеныНоменклатуры.ЦенаПродажи КАК ЦенаПродажи
	               |ИЗ
	               |	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	               |ГДЕ
	               |	ЦеныНоменклатуры.Контрагент = &Контрагент
	               |	И ЦеныНоменклатуры.Период = &Период
	               |	И ЦеныНоменклатуры.Номенклатура В(&Номенклатура)
	               |	И ЦеныНоменклатуры.Регистратор <> &Регистратор";
	
	МассивНоменклатуры = Товары.ВыгрузитьКолонку("Номенклатура");
	Запрос.Параметры.Вставить("Контрагент",Контрагент);
	Запрос.Параметры.Вставить("Период",НачалоДня(Дата));
	Запрос.Параметры.Вставить("Номенклатура",МассивНоменклатуры);
	Запрос.Параметры.Вставить("Регистратор",Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		// что-то установлено на дату документа.
		// составим текст сообщения пользователю
		ТекстСообщения = "";
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСтроки = "Для номенклатуры "+Выборка.Номенклатура+" уже установлена цена "+Выборка.ЦенаПродажи+" на дату "+Выборка.Период;
			Если ЗначениеЗаполнено(ТекстСообщения) Тогда
				ТекстСообщения = ТекстСообщения + Символы.ПС;
			КонецЕсли;
			ТекстСообщения = ТекстСообщения + ТекстСтроки;
		КонецЦикла;
		Сообщить(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры
