
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// запишем расход, номенклатуру соберем с помощью запроса
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТовараТовары.Номенклатура КАК Номенклатура,
	|	СУММА(РеализацияТовараТовары.Количество) КАК Количество,
	|	СУММА(РеализацияТовараТовары.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.РеализацияТовара.Товары КАК РеализацияТовараТовары
	|ГДЕ
	|	РеализацияТовараТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТовараТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтТовары.Номенклатура КАК Номенклатура,
	|	ВтТовары.Количество КАК Остаток,
	|	&ВидДвижения КАК ВидДвижения,
	|	&Период КАК Период
	|ИЗ
	|	ВтТовары КАК ВтТовары";
	
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина; // мы собираемся проверять остаток, поэтому накладываем блокировку
	Движения.ОстаткиТоваров.Загрузить(РезультатЗапроса.Выгрузить());
	
	// запишем движения, чтобы проверить остатки
	Движения.Записать();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
	|	-ОстаткиТоваровОстатки.ОстатокОстаток КАК Остаток
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваров.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВтТовары.Номенклатура КАК Номенклатура
	|				ИЗ
	|					ВтТовары КАК ВтТовары)) КАК ОстаткиТоваровОстатки
	|ГДЕ
	|	ОстаткиТоваровОстатки.ОстатокОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая)));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает товара " + Выборка.Номенклатура + " в количестве " + Выборка.Остаток;
			Сообщение.Сообщить();
			
		КонецЦикла;
		
	КонецЕсли;
	
	// остатка не хватает, не проводим
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;
	
	// запишем продажи
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВтТовары.Номенклатура КАК Номенклатура,
	|	ВтТовары.Количество КАК Количество,
	|	&Период КАК Период,
	|	ВтТовары.Сумма КАК Сумма,
	|	&Контрагент КАК Контрагент
	|ИЗ
	|	ВтТовары КАК ВтТовары";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.Продажи.Записывать = Истина;
	Движения.Продажи.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры
