Перем МассивСтепеней;
Перем ОписаниеКопеек;
Перем МассивИменСотен;
Перем МассивИменДесятков;
Перем МассивИменВторогоДесятка;
Перем МассивИменЕдиницМ;
Перем МассивИменЕдиницЖ;

#Область ПрограммныйИнтерфейс

Функция ПолучитьПрописьЧисла(Знач ОбрабатываемоеЧисло) Экспорт
	
	Копейки = 100*(ОбрабатываемоеЧисло - Цел(ОбрабатываемоеЧисло));
	ОбрабатываемоеЧисло = Цел(ОбрабатываемоеЧисло);
	
	// разбиваем число по разрядам. Должны получить массив чисел от 1 до 1000, нарезанные по разрядам
	
	МассивРазрядности = Новый Массив;
	МассивРазрядности.Добавить(Копейки);// самый низший разряд
	
	Пока ОбрабатываемоеЧисло > 0 Цикл
		МассивРазрядности.Вставить(0,ОбрабатываемоеЧисло % 1000);
		ОбрабатываемоеЧисло = Цел(ОбрабатываемоеЧисло/1000);
	КонецЦикла;
	
	// массив в строку
	ВозвращаемоеЗначение = "";
	НомерСтаршегоРазряда = МассивРазрядности.Количество();
	Для каждого ЭлементМассива Из МассивРазрядности Цикл
		Если ЭлементМассива = 0 Тогда
			НомерСтаршегоРазряда = НомерСтаршегоРазряда - 1;
			Продолжить;// пропустим, ибо ноль
		КонецЕсли;
		НаписаниеСтепени = "";
		// получим написание степени из описания
		ОписаниеСтепени = МассивСтепеней[НомерСтаршегоРазряда-1];
		Если ЭлементМассива = 1 Тогда
			НаписаниеСтепени = ОписаниеСтепени.Значение1;
		ИначеЕсли ЭлементМассива < 5 Тогда
			НаписаниеСтепени = ОписаниеСтепени.Значение2;
		ИначеЕсли ЭлементМассива < 21 Тогда
			НаписаниеСтепени = ОписаниеСтепени.Значение5;
		ИначеЕсли ЭлементМассива%10 = 1 Тогда
			НаписаниеСтепени = ОписаниеСтепени.Значение1;
		ИначеЕсли (ЭлементМассива%10 = 2) ИЛИ (ЭлементМассива%10 = 3) ИЛИ (ЭлементМассива%10 = 4) Тогда
			НаписаниеСтепени = ОписаниеСтепени.Значение2;
		Иначе
			НаписаниеСтепени = ОписаниеСтепени.Значение5;
		КонецЕсли;
		ВозвращаемоеЗначение = ВозвращаемоеЗначение + ПолучитьПропись1000(ЭлементМассива, ОписаниеСтепени.Род) + НаписаниеСтепени + " ";
		НомерСтаршегоРазряда = НомерСтаршегоРазряда - 1;
	КонецЦикла;
	
	// не помню, было ли в задании, что первый символ должен быть заглавный. Но давай сделаем, чтобы красиво
	Возврат Врег(Лев(ВозвращаемоеЗначение,1))+Сред(ВозвращаемоеЗначение,2);
	
	//Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область Служебные

Функция ПолучитьПропись1000(Знач ОбрабатываемоеЧисло, Род = "М")
	
	// так. Делаем по другому. Отделяем сотню и разбираемся с двухзначным
	Сотни = Цел(ОбрабатываемоеЧисло/100);
	Результат = МассивИменСотен[Сотни];
	ОбрабатываемоеЧисло = ОбрабатываемоеЧисло-Сотни*100;
	Если ОбрабатываемоеЧисло < 20 И ОбрабатываемоеЧисло > 10 Тогда
		// если второй десяток - сразу вернем строку
		Результат = Результат + МассивИменВторогоДесятка[ОбрабатываемоеЧисло-11];
	Иначе
		// тут разберем на части и слепим строку
		Единицы = ОбрабатываемоеЧисло % 10;
		Десятки = Цел(ОбрабатываемоеЧисло/10);
		
		Результат = Результат + МассивИменДесятков[Десятки];
		Если Род="М" Тогда
			Результат = Результат + МассивИменЕдиницМ[Единицы];
		Иначе
			Результат = Результат + МассивИменЕдиницЖ[Единицы];
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОписаниеСтепени()
	Описание = Новый Структура();
	
	Описание.Вставить("Имя","");
	Описание.Вставить("Род","М");
	Описание.Вставить("Значение1","");
	Описание.Вставить("Значение2","");
	Описание.Вставить("Значение5","");
	
	Возврат Описание;
	
КонецФункции

Процедура Инициализация()
	
	// массивы имен
	МассивИменСотен = Новый Массив;
	МассивИменСотен.Добавить("");
	МассивИменСотен.Добавить("сто ");
	МассивИменСотен.Добавить("двести ");
	МассивИменСотен.Добавить("триста ");
	МассивИменСотен.Добавить("четыреста ");
	МассивИменСотен.Добавить("пятьсот ");
	МассивИменСотен.Добавить("шестьсот ");
	МассивИменСотен.Добавить("семьсот ");
	МассивИменСотен.Добавить("восемьсот ");
	МассивИменСотен.Добавить("девятьсот ");
	
	МассивИменДесятков = Новый Массив;
	МассивИменДесятков.Добавить("");
	МассивИменДесятков.Добавить("десять ");
	МассивИменДесятков.Добавить("двадцать ");
	МассивИменДесятков.Добавить("тридцать ");
	МассивИменДесятков.Добавить("сорок ");
	МассивИменДесятков.Добавить("пятьдесят ");
	МассивИменДесятков.Добавить("шестьдесят ");
	МассивИменДесятков.Добавить("семьдесят ");
	МассивИменДесятков.Добавить("восемьдесят ");
	МассивИменДесятков.Добавить("девяносто ");
	
	МассивИменЕдиницМ = Новый Массив;
	МассивИменЕдиницМ.Добавить("");
	МассивИменЕдиницМ.Добавить("один ");
	МассивИменЕдиницМ.Добавить("два ");
	МассивИменЕдиницМ.Добавить("три ");
	МассивИменЕдиницМ.Добавить("четыре ");
	МассивИменЕдиницМ.Добавить("пять ");
	МассивИменЕдиницМ.Добавить("шесть ");
	МассивИменЕдиницМ.Добавить("семь ");
	МассивИменЕдиницМ.Добавить("восемь ");
	МассивИменЕдиницМ.Добавить("девять ");
	
	МассивИменЕдиницЖ = Новый Массив;
	МассивИменЕдиницЖ.Добавить("");
	МассивИменЕдиницЖ.Добавить("одна ");
	МассивИменЕдиницЖ.Добавить("две ");
	МассивИменЕдиницЖ.Добавить("три ");
	МассивИменЕдиницЖ.Добавить("четыре ");
	МассивИменЕдиницЖ.Добавить("пять ");
	МассивИменЕдиницЖ.Добавить("шесть ");
	МассивИменЕдиницЖ.Добавить("семь ");
	МассивИменЕдиницЖ.Добавить("восемь ");
	МассивИменЕдиницЖ.Добавить("девять ");
	
	МассивИменВторогоДесятка = Новый Массив;
	МассивИменВторогоДесятка.Добавить("одиннадцать ");
	МассивИменВторогоДесятка.Добавить("двенадцать ");
	МассивИменВторогоДесятка.Добавить("тринадцать ");
	МассивИменВторогоДесятка.Добавить("четырнадцать ");
	МассивИменВторогоДесятка.Добавить("пятнадцать ");
	МассивИменВторогоДесятка.Добавить("шестнадцать ");
	МассивИменВторогоДесятка.Добавить("семнадцать ");
	МассивИменВторогоДесятка.Добавить("восемнадцать ");
	МассивИменВторогоДесятка.Добавить("девятнадцать ");
	
	// заполним соответствие степеней
	МассивСтепеней = Новый Массив;
	
	// Копейки
	ОписаниеКопеек = ПолучитьОписаниеСтепени();
	ОписаниеКопеек.Имя = "Копейка";
	ОписаниеКопеек.Род = "Ж";
	ОписаниеКопеек.Значение1 = "копейка";
	ОписаниеКопеек.Значение2 = "копейки";
	ОписаниеКопеек.Значение5 = "копеек";
	МассивСтепеней.Добавить(ОписаниеКопеек);
	
	// рубли
	ОписаниеСтепени = ПолучитьОписаниеСтепени();
	ОписаниеСтепени.Имя = "Рубли";
	ОписаниеСтепени.Род = "М";
	ОписаниеСтепени.Значение1 = "рубль";
	ОписаниеСтепени.Значение2 = "рубля";
	ОписаниеСтепени.Значение5 = "рублей";
	МассивСтепеней.Добавить(ОписаниеСтепени);
	
	ОписаниеСтепени = ПолучитьОписаниеСтепени();
	ОписаниеСтепени.Имя = "Тысяча";
	ОписаниеСтепени.Род = "Ж";
	ОписаниеСтепени.Значение1 = "тысяча";
	ОписаниеСтепени.Значение2 = "тысячи";
	ОписаниеСтепени.Значение5 = "тысяч";
	МассивСтепеней.Добавить(ОписаниеСтепени);
	
	ОписаниеСтепени = ПолучитьОписаниеСтепени();
	ОписаниеСтепени.Имя = "Миллион";
	ОписаниеСтепени.Род = "М";
	ОписаниеСтепени.Значение1 = "миллион";
	ОписаниеСтепени.Значение2 = "миллиона";
	ОписаниеСтепени.Значение5 = "миллионов";
	МассивСтепеней.Добавить(ОписаниеСтепени);
	
	ОписаниеСтепени = ПолучитьОписаниеСтепени();
	ОписаниеСтепени.Имя = "Миллиард";
	ОписаниеСтепени.Род = "М";
	ОписаниеСтепени.Значение1 = "миллиард";
	ОписаниеСтепени.Значение2 = "миллиарда";
	ОписаниеСтепени.Значение5 = "миллиардов";
	МассивСтепеней.Добавить(ОписаниеСтепени);
	
	ОписаниеСтепени = ПолучитьОписаниеСтепени();
	ОписаниеСтепени.Имя = "Триллион";
	ОписаниеСтепени.Род = "М";
	ОписаниеСтепени.Значение1 = "триллион";
	ОписаниеСтепени.Значение2 = "триллиона";
	ОписаниеСтепени.Значение5 = "триллионов";
	МассивСтепеней.Добавить(ОписаниеСтепени);
	
	ОписаниеСтепени = ПолучитьОписаниеСтепени();
	ОписаниеСтепени.Имя = "Квадриллион";
	ОписаниеСтепени.Род = "М";
	ОписаниеСтепени.Значение1 = "квадриллион";
	ОписаниеСтепени.Значение2 = "квадриллиона";
	ОписаниеСтепени.Значение5 = "квадриллионов";
	МассивСтепеней.Добавить(ОписаниеСтепени);
	
	ОписаниеСтепени = ПолучитьОписаниеСтепени();
	ОписаниеСтепени.Имя = "Квинтиллион";
	ОписаниеСтепени.Род = "М";
	ОписаниеСтепени.Значение1 = "квинтиллион";
	ОписаниеСтепени.Значение2 = "квинтиллиона";
	ОписаниеСтепени.Значение5 = "квинтиллионов";
	МассивСтепеней.Добавить(ОписаниеСтепени);
	
КонецПроцедуры

#КонецОбласти

Инициализация();